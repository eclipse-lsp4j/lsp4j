/******************************************************************************
 * Copyright (c) 2016 TypeFox and others.
 * 
 * This program and the accompanying materials are made available under the
 * terms of the Eclipse Public License v. 2.0 which is available at
 * http://www.eclipse.org/legal/epl-2.0,
 * or the Eclipse Distribution License v. 1.0 which is available at
 * http://www.eclipse.org/org/documents/edl-v10.php.
 * 
 * SPDX-License-Identifier: EPL-2.0 OR BSD-3-Clause
 ******************************************************************************/

import java.time.format.DateTimeFormatter
import java.time.LocalDateTime

buildscript {
	repositories.jcenter()
	dependencies {
		classpath 'org.xtext:xtext-gradle-plugin:2.0.7'
		classpath 'io.typefox.gradle:gradle-p2gen:0.1.0'
	}
}

ext.buildTime = DateTimeFormatter.ofPattern('yyyyMMdd-HHmm').format(LocalDateTime.now())

apply from: "${rootDir}/gradle/versions.gradle"
apply from: "${rootDir}/gradle/p2-deployment.gradle"

subprojects {
	repositories.jcenter()
	
	group = 'org.eclipse.lsp4j'
	version = rootProject.version
	
	apply plugin: 'java'
	apply plugin: 'org.xtext.xtend'
	apply plugin: 'eclipse'
	apply plugin: 'maven'
	
	apply from: "${rootDir}/gradle/java-compiler-settings.gradle"
	apply from: "${rootDir}/gradle/xtend-compiler-settings.gradle"
	apply from: "${rootDir}/gradle/maven-deployment.gradle"
	apply from: "${rootDir}/gradle/manifest-gen.gradle"
}

task clean(type: Delete) {
	group 'Build'
	description 'Deletes the local repositories'
	delete 'build'
}

// Generate an IDE with Xtend support
task ide(type: GradleBuild) {
	buildFile = 'ide/build.gradle'
	tasks = ['ide']
}
