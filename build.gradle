/******************************************************************************
 * Copyright (c) 2016 TypeFox and others.
 * 
 * This program and the accompanying materials are made available under the
 * terms of the Eclipse Public License v. 2.0 which is available at
 * http://www.eclipse.org/legal/epl-2.0,
 * or the Eclipse Distribution License v. 1.0 which is available at
 * http://www.eclipse.org/org/documents/edl-v10.php.
 * 
 * SPDX-License-Identifier: EPL-2.0 OR BSD-3-Clause
 ******************************************************************************/

import java.time.format.DateTimeFormatter
import java.time.LocalDateTime

buildscript {
	repositories {
		mavenCentral()
		maven {
			url "https://plugins.gradle.org/m2/"
		}
	}
	dependencies {
		classpath "biz.aQute.bnd:biz.aQute.bnd.gradle:6.3.1"
		classpath "org.xtext:xtext-gradle-plugin:2.1.0"
	}
}

ext.buildTime = DateTimeFormatter.ofPattern('yyyyMMdd-HHmm').format(LocalDateTime.now())

apply from: "${rootDir}/gradle/versions.gradle"

subprojects {
	repositories.mavenCentral()
	
	group = 'org.eclipse.lsp4j'
	version = rootProject.version
	
	apply plugin: 'java-library'
	apply plugin: 'org.xtext.xtend'
	apply plugin: 'eclipse'
	apply plugin: 'maven-publish'
	
	apply from: "${rootDir}/gradle/java-compiler-settings.gradle"
	apply from: "${rootDir}/gradle/xtend-compiler-settings.gradle"
	apply from: "${rootDir}/gradle/maven-deployment.gradle"
	apply from: "${rootDir}/gradle/manifest-gen.gradle"

	configurations.all {
		if (name.contains("Tooling")) {
			dependencies {
				add(name, "org.eclipse.xtend:org.eclipse.xtend.core:${versions.xtend_lib}")
				add(name, platform("org.eclipse.xtext:xtext-dev-bom:${versions.xtend_lib}"))
			}
		}
	}
}

task clean(type: Delete) {
	group 'Build'
	description 'Deletes the local repositories'
	delete 'build'
}

// Generate an IDE with Xtend support
task ide(type: GradleBuild) {
	buildFile = 'ide/build.gradle'
	tasks = ['ide']
}
