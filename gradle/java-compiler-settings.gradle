/******************************************************************************
 * Copyright (c) 2016 TypeFox and others.
 * 
 * This program and the accompanying materials are made available under the
 * terms of the Eclipse Public License v. 2.0 which is available at
 * http://www.eclipse.org/legal/epl-2.0,
 * or the Eclipse Distribution License v. 1.0 which is available at
 * http://www.eclipse.org/org/documents/edl-v10.php.
 * 
 * SPDX-License-Identifier: EPL-2.0 OR BSD-3-Clause
 ******************************************************************************/

// Use central version from gradle/versions.gradle

// --- Enforce Java version for running Gradle itself ---
def currentJava = JavaVersion.current()
def minJava = JavaVersion.VERSION_17 // xtend requires Java >= 17
def maxJava = JavaVersion.VERSION_21 // Gradle 9.x is required for Java 25

if (currentJava < minJava || currentJava > maxJava) {
    throw new GradleException("""
This build must be run with Java between ${minJava} and ${maxJava}.

Current Java:
  JavaVersion.current() = ${currentJava}
  java.version          = ${System.getProperty('java.version')}
  java.home             = ${System.getProperty('java.home')}

Please switch your JAVA_HOME or use the Gradle wrapper with a compatible JDK.

In addition, to run testOlderJavas tasks Java 17 and Java 11 must be discovrable.
See https://docs.gradle.org/8.6/userguide/toolchains.html#sec:auto_detection
""")
}

java {
	sourceCompatibility = JavaVersion.toVersion(versions.java)
	targetCompatibility = JavaVersion.toVersion(versions.java)
}

tasks.withType(Javadoc) {
	options.addStringOption('Xdoclint:none', '-quiet')
	options.encoding = 'UTF-8'
}

tasks.withType(JavaCompile) {
	options.encoding = 'UTF-8'
	// Ensure cross-compilation targets specified Java version
    options.release = versions.java as Integer
}

task sourcesJar(type: Jar, dependsOn: classes) {
	group = 'Build'
	description = 'Assembles a jar archive containing the sources.'
	archiveClassifier = 'sources'
	from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
	group = 'Build'
	description = 'Assembles a jar archive containing the JavaDoc output.'
	archiveClassifier = 'javadoc'
	from javadoc.destinationDir
}

ext.signMethod = { jarfile -> 
	println "Signing $jarfile"
	def SIGNING_SERVICE = 'https://cbi.eclipse.org/jarsigner/sign'
	def STDOUT_FORMAT = '    %{size_upload} bytes uploaded, %{size_download} bytes downloaded (%{time_total} s)\\n'

	ProcessBuilder curl_pb = new ProcessBuilder("curl", 
		"--fail", "--silent", "--show-error", "--output",
		"${jarfile}-signed", "--form", "file=@${jarfile}",
		"--write-out", STDOUT_FORMAT, 
		SIGNING_SERVICE);
	println String.join(" ", curl_pb.command());
	curl_pb.directory(new File("${project.buildDir}"));
	Process curl_process = curl_pb.start()
	curl_process.waitFor()

	println curl_process.err.text
	println curl_process.text

	if (curl_process.exitValue() != 0) {
		throw new GradleException("Failed to run curl"); 
	}

	ProcessBuilder mv_pb = new ProcessBuilder("mv", 
		"${jarfile}-signed", jarfile)
	println String.join(" ", mv_pb.command());
	mv_pb.directory(new File("${project.buildDir}"));
	Process mv_process = mv_pb.start()
	mv_process.waitFor()

	if (curl_process.exitValue() != 0) {
		throw new GradleException("Failed to run mv"); 
	}
}

task signJar(description: 'Sign JARs with Eclipse Signing Service', group: 'Build'){
    doLast {
		signMethod("${project.buildDir}/libs/${project.name}-${project.version}.jar")
		signMethod("${project.buildDir}/libs/${project.name}-${project.version}-sources.jar")
		signMethod("${project.buildDir}/libs/${project.name}-${project.version}-javadoc.jar")
	}
}
signJar.dependsOn jar, sourcesJar, javadocJar

artifacts.archives sourcesJar, javadocJar

if (findProperty('ignoreTestFailures') == 'true') {
	tasks.withType(Test) {
		ignoreFailures = true
	}
}

def testDir = file('src/test/java')
def xtendTestClassPatterns = []

if (testDir.exists()) {
	testDir.eachFileRecurse { File f ->
		if (f.name.endsWith('.xtend')) {
			// Path relative to src/test/java, with Unix-ish separators
			def rel = testDir.toPath().relativize(f.toPath()).toString()
			rel = rel.replace(File.separatorChar, '/' as char)

			// Replace .xtend with .class, wildcard anything before it
			// e.g. com/foo/BarTest.xtend -> **/com/foo/BarTest.class
			def classPattern = "**/${rel[0..-6]}class"
			xtendTestClassPatterns << classPattern
		}
	}
}

tasks.register('testJava11', Test) {
	description = 'Runs non-xtend tests on Java 11 JVM'
	group = 'verification'

	testClassesDirs = sourceSets.test.output.classesDirs
	classpath = sourceSets.test.runtimeClasspath

	// Current version of xtend require JVM >= 17 to run, 
	// so exclude xtend based tests
	if (!xtendTestClassPatterns.isEmpty()) {
		exclude xtendTestClassPatterns
	}

	javaLauncher.set(
		javaToolchains.launcherFor {
			languageVersion = JavaLanguageVersion.of(11)
		}
	)
}

tasks.register('testJava17', Test) {
	description = 'Runs tests on Java 17 JVM'
	group = 'verification'

	testClassesDirs = sourceSets.test.output.classesDirs
	classpath = sourceSets.test.runtimeClasspath

	javaLauncher.set(
		javaToolchains.launcherFor {
			languageVersion = JavaLanguageVersion.of(17)
		}
	)
}

tasks.register('testOlderJavas') {
	dependsOn tasks.named('testJava17'), tasks.named('testJava11')
}